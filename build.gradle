plugins {
    id 'application'
}

application {
    mainClass = 'Main'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
    intTest {
        java {
            srcDirs = ['src/intTest/java']
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

tasks.named('test') {
    enabled = false
}

tasks.named('testClasses') {
    enabled = false
}

tasks.named('compileTestJava') {
    enabled = false
}

tasks.named('processTestResources') {
    enabled = false
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

run {
    standardInput = System.in
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    intTestImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    intTestImplementation 'org.mockito:mockito-core:5.11.0'
    intTestRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': 'Main'
    }
    archiveBaseName = 'client04'
}

tasks.register('copyToBin', Copy) {
    dependsOn jar
    from 'build/libs'
    into 'bin'
    include '*.jar'
}

tasks.register('deleteBin', Delete){
    delete 'bin'
}

jar.finalizedBy('copyToBin')
clean.finalizedBy('deleteBin')

tasks.register('cleanOldLogFiles') {
    doLast {
        def logDir = file('binaries/logArchive')
        logDir.mkdirs()

        // Search the entire project directory
        def projectDir = file('.')
        def logFiles = []

        projectDir.eachFileRecurse { file ->
            if (file.name.matches(/log_game_.*\.txt/)) {
                logFiles << file
            }
        }

        // Move files to the log archive directory
        logFiles.each { file ->
            def newFile = new File(logDir, file.name as String)
            file.renameTo(newFile)
            println("Moved log file: ${file.name}")
        }

        // List and sort log files in the archive directory by last modified date (descending)
        def archivedLogFiles = logDir.listFiles().findAll { it.isFile() && it.name.startsWith('log_game_') }
        archivedLogFiles.sort { -it.lastModified() }

        // Determine files to delete if the count exceeds the keep count
        def keepCount = 50
        def filesToDelete = archivedLogFiles.size() > keepCount ? archivedLogFiles.drop(keepCount) : []

        // Delete old log files
        filesToDelete.each { file ->
            if (file.delete()) {
                println("Deleted old log file: ${file.name}")
            } else {
                println("Failed to delete old log file: ${file.name}")
            }
        }
    }
}

intTestClasses.finalizedBy(cleanOldLogFiles)